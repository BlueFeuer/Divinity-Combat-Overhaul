#INCLUDE Base
INIT
CHARACTER:__Me
FIXEDSTRING:%ForceAnim = ""
TRIGGER:%CreationPoint = null
FIXEDSTRING:%CCIntroductionAnim = ""
FIXEDSTRING:%CCIdleAnim = "still"


	USING SHARED Base

EVENTS
EVENT CharacterCreationStarted
VARS 
	TRIGGER:_CreationTrigger
	FIXEDSTRING:_Class
ON 
	OnCharacterCreationStarted(__Me, _CreationTrigger)
ACTIONS
	CharacterSetAnimationSetOverride(__Me,"CharCreation")
	IF "c1&c2"
		CharacterGetCreationClass(__Me,_Class)
		IsEqual(_Class,"Alchemist")
	THEN
		Set(%CCIdleAnim,"CC_Still_Gun")
	ELSE
		Set(%CCIdleAnim,"still")
	ENDIF
	Interrupt("IdleCharacterCreation")
		
	SetPriority("IdleCharacterCreation", 1000000)
	SetPriority("CharacterCreationMoveToTrigger", 1000002)
	Set(%CreationPoint, _CreationTrigger)

EVENT CharacterCreationStopped
ON 
	OnCharacterCreationStopped(__Me)
ACTIONS
	CharacterSetAnimationSetOverride(__Me,"")
	SetPriority("IdleCharacterCreation", 0)
	SetPriority("CharacterCreationMoveToTrigger", 0)
	//SetPriority("NewClassCharacterCreation", 0)
	CharacterStopAnimation()
	
EVENT OnOriginIntroductionStopped
ON
	OnCharacterEvent(__Me,"GLO_StopOriginIntroduction")
ACTIONS
	Interrupt("DoOriginIntroduction")
	SetPriority("ExitOriginIntroNoBlend",1000002)
	
EVENT OnOriginIntroductionStarted
ON
	OnCharacterEvent(__Me,"GLO_StartOriginIntroduction")
ACTIONS
	IF "!c1"
		IsEqual(%CCIntroductionAnim,"")
	THEN
		SetPriority("DoOriginIntroduction",1000003)
	ENDIF
 
EVENT CharacterClassChanged
VARS 
	FIXEDSTRING:_Class
ON 
	OnCharacterClassChanged(__Me, _Class)
ACTIONS
	IF "c1"
		IsEqual(_Class,"Alchemist") 
	THEN
		Set(%CCIdleAnim,"CC_Still_Gun")
	ELSE
		Set(%CCIdleAnim,"still")
	ENDIF
	Interrupt("IdleCharacterCreation")
		
BEHAVIOUR

REACTION DoOriginIntroduction, 0
USAGE PEACE
ACTIONS
	CharacterSetFightMode(__Me,0,1)
	CharacterPlayAnimation(%CCIntroductionAnim,1, 1, 1)
	CharacterEvent(__Me,"OriginIntroductionFinished")
	CharacterSetFightMode(__Me,1,1)
	SetPriority("DoOriginIntroduction",0)
	SetPriority("ExitOriginIntroNoBlend",1000002)

INTERRUPT
ACTIONS
	Reset()
	SetPriority("DoOriginIntroduction",0)
	CharacterSetFightMode(__Me,1,1)

REACTION ExitOriginIntroNoBlend, 0
USAGE PEACE
ACTIONS
CharacterPlayAnimation("still", 1, 1, 1)
SetPriority("ExitOriginIntroNoBlend",0)

REACTION CharacterCreationMoveToTrigger, 0
USAGE PEACE
VARS 
CHECK "c1&!c2"
	CharacterInCreation(__Me, _)
	IsEqual(%CreationPoint, null)
ACTIONS		
	CharacterPlayAnimation(%CCIdleAnim,0,0,1)
	TeleportTo(__Me,%CreationPoint)
	CharacterLookFrom(%CreationPoint, 1)
	Set(%CreationPoint, null)

REACTION IdleCharacterCreation, 0
USAGE PEACE
CHECK "c1"
	CharacterInCreation(__Me, _)
ACTIONS		
	CharacterPlayAnimation(%CCIdleAnim,0)
INTERRUPT
ACTIONS
	Reset()
	
